contract ChainSingleStart {
    data {
        Object int
        Template int
    }
    conditions {
        var admin int
        admin = Int(EcosysParam("role_admin"))
        if !RoleAccess(admin){
            warning LangRes("@1access_denied", "en")
        }

        $templateName = DBFind("@1supply_templates").Where({"id":$Template}).Columns("id,name").One("name")
        $objectName = DBFind("@1supply_objects").Where({"id":$Object}).Columns("id,name").One("name")
        $recipientID = Int(DBFind("@1supply_templates").Where({"id":$Template}).Columns("chain->recipient_id").One("chain.recipient_id"))
        $senderID = Int(DBFind("@1supply_templates").Where({"id":$Template}).Columns("chain->sender_id").One("chain.sender_id"))
    }
    action {
    var bt string m map
    bt = BlockTime()
    m["object_id"] = $Object
    m["template_id"] = $Template
    m["template_name"] = $templateName
    m["object_name"] = $objectName
    m["status"] = 1
    m["date_start"] = bt
    m["recipients_id"] = $recipientID
    m["senders_id"] = $senderID
    $chainID = DBInsert("@1supply_chains", m)
    $result = $chainID

    var n map
    n["chain_id"] = $chainID 
    n["recipient_ID"] = $recipientID
    n["sender_ID"] = $senderID
    DBInsert("@1supply_stages", n)
    
    @1SupplyNotificSend("chain_id,sender_role_id,recipient_role_id",$chainID,$senderID,$recipientID)
    }
}