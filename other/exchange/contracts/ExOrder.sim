contract ExOrder {
    data {
        client int
        flag string
        symbol_id int
        quantity int
        price money
    }
    conditions {
        var symbol_map map
        symbol_map = DBFind("@1symbol").Where({"id":$symbol_id}).Row()  
        if !symbol_map {
            warning Sprintf("%v", "not found")
        }
        if Int(symbol_map["tradeable"]) != 1 {
            warning LangRes("@1ex_error_tradestatus", "en")
        }
        $ecosystem_hub = Int(symbol_map["ecosystem_hub"])
        if !DBFind("@1ecosystems").Where({"id":$ecosystem_hub}).One("id") {
            warning Sprintf(LangRes("@1ecosystem_not_found", "en"), $ecosystem_hub)
        }
        if !DBFind("@1keys").Where({"id":$client, "ecosystem":$ecosystem_hub}).One("id") {
            warning LangRes("@1ex_error_account", "en")
        }
        var balance value money
        balance = Money(DBFind("@1keys").Where({"id":$client, "ecosystem": $ecosystem_hub}).Columns("amount").One("amount"))
        if $flag == "S" {
            var value money
            value = Money($quantity)*Money(symbol_map["lotsize"])
            if value > balance {
                warning LangRes("@1ex_error_balance_check", "en")
            }
        }
        if $flag == "B" {
            var value money
            value = Money($price)*Money($quantity)
            if value > balance {
                warning LangRes("@1ex_error_balance_check", "en")
            }
        }
        //warning LangRes("@1ex_error_market_orders", "en")
    }
    action {
        var symbol_map map
        symbol_map = DBFind("@1symbol").Where({"id":$symbol_id}).Row()

        var m map
        m["flag"] = $flag
        m["client"] = $client
        m["venue"] = "EX"
        m["symbol"] = Str(symbol_map["name"])
        m["quantity"] = $quantity
        m["price"] = $price 
        m["primary_value"] = Money($price)*Money($quantity)
        m["face_value"] = Money($quantity)*Money(symbol_map["lotsize"])
        m["status"] = "A"
        m["lastmodified"] = $time
        DBInsert("@1order_log", m)
        
        if $flag == "S" {
            var value money
            value = Money($quantity)*Money(symbol_map["lotsize"])
            @1TokensDeposit("Deposit,Ecosystem", value, Int(symbol_map["ecosystem_hub"]))
        }
        if $flag == "B" {
            var value money
            value = Money($price)*Money($quantity)
            @1TokensDeposit("Deposit,Ecosystem", value, Int(symbol_map["primary_hub"]))
        }
    }
}