contract ExSymbol {
    data {
        id int "optional"
        status string "optional"
        operation string
        name string
        pricetick money
        range money
        lotsize money
        primary_hub int
        ecosystem_hub int
        isTradeable int "optional"
        isCross int "optional"
        isOTC int "optional"
    }
    conditions {
        if $operation == "C" {
            var check_symbol string
            check_symbol = DBFind("@1symbol").Where({"name":$name}).Columns("name").One("name")
            if (check_symbol == $name) {
                warning LangRes("@1ex_error_symbol_name", "en")
            }
        }
        if ($primary_hub == $ecosystem_hub) {
            warning LangRes("@1ex_error_mirror_trading", "en")
        }
        if !DBFind("@1ecosystems").Where({"id": $primary_hub}).One("id") {
            warning Sprintf(LangRes("@1ecosystem_not_found", "en"), $primary_hub)
        }
        if !DBFind("@1ecosystems").Where({"id": $ecosystem_hub}).One("id") {
            warning Sprintf(LangRes("@1ecosystem_not_found", "en"), $ecosystem_hub)
        }
        ContractConditions("@1AdminCondition") 
    }
    action {
        if $operation == "C" {
            var m map
            m["name"] = $name + Str("ECO") //fix auto naming
            m["pricetick"] = $pricetick
            m["range"] = $range
            m["lotsize"] = $lotsize
            m["tradeable"] = $isTradeable
            m["cross"] = $isCross
            m["otc"] = $isOTC
            m["primary_hub"] = $primary_hub
            m["ecosystem_hub"] = $ecosystem_hub
            DBInsert("@1symbol", m)
        }
        if $operation == "E" {
            @1ExCancel("symbol", $name) //by id
            var m map
            m["pricetick"] = $pricetick
            m["range"] = $range
            m["lotsize"] = $lotsize
            m["ecosystem_hub"] = $ecosystem_hub
            DBUpdate("@1symbol", $id, m)
        }
        if $operation == "U" {
            var m map
            if $status == "TR" {
                //@1ExCancel("symbol", $name) by id
                m["tradeable"] = $isTradeable
            } 
            if $status == "CR" {
                m["cross"] = $isCross
            }
            if $status == "OTC" {
                m["otc"] = $isOTC
            }
            DBUpdate("@1symbol", $id, m)
        }
    }
}