contract LrDepartRequest {
    data {
        ObjectId int
        DepartNumber int
    }

    conditions {
        $app_id = DBFind("@1applications").Where({"ecosystem": $ecosystem_id, "name": "Land registry"}).One("id")
        var requests_flag int
        if $DepartNumber == 2 {
            requests_flag = DBFind("@1notifications").Where({"ecosystem": $ecosystem_id, "page_params->object_id": $ObjectId, "page_params->depart2_requests": 2}).One("id")
            if requests_flag {
                warning LangRes("lr_requests_limit_reached", "en")
            }
        } elif $DepartNumber == 3 {
            requests_flag = DBFind("@1notifications").Where({"ecosystem": $ecosystem_id, "page_params->object_id": $ObjectId, "page_params->depart3_requests": 2}).One("id")
            if requests_flag {
                warning LangRes("lr_requests_limit_reached", "en")
            }
        }
    }

    action {
        var request params map
        request["Sender"] = 1
        request["Page"] = "lr_view"
        request["Header"] = LangRes("lr_additional_request", "en")
        request["Body"] = LangRes("lr_need_processing", "en")
        request["Icon"] = "fa fa-bell-o"
        request["Closure"] = 1
        request["Popup"] = "true"
        params["object_id"] = $ObjectId

        if $DepartNumber == 2 {
            request["Rid"] = Int(AppParam(Int($app_id), "lr_depart2_role", $ecosystem_id))
            params["depart2_requests"] = 2
            request["Params"] = params
        } elif $DepartNumber == 3 {
            request["Rid"] = Int(AppParam(Int($app_id), "lr_depart3_role", $ecosystem_id))
            params["depart3_requests"] = 2
            request["Params"] = params
        }

        CallContract("@1NotificationsSend", request)
    }
}