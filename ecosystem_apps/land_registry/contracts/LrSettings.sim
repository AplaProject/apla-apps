contract LrSettings {
    data {
        RoleAdmin int
        RoleBank int
        RoleDepart2 int
        RoleDepart3 int
    }

    conditions{
        AdminCondition()
    }

    func updateParam(name string, id int) {
        if id > 0 {
            var param paramsNew map app_id int
            app_id = DBFind("@1applications").Where({ecosystem: $ecosystem_id, name: "Land registry"}).One("id")
            if app_id > 0 {
                param = DBFind("@1app_params").Where({ecosystem: $ecosystem_id, app_id: app_id, name: name}).Row()
                paramsNew["Value"] = Str(id)
                if param {
                    paramsNew["Id"] = Int(param["id"])
                    paramsNew["Conditions"] = param["conditions"]
                    CallContract("@1EditAppParam", paramsNew)
                } else {
                    paramsNew["App"] = Str(app_id)
                    paramsNew["Name"] = name
                    paramsNew["Conditions"] = "ContractConditions(\"AdminCondition\")"
                    CallContract("@1NewAppParam", paramsNew)
                }
            } else {
                warning LangRes("@1app_not_found", "en")
            }
        }
    }

    action {
        updateParam("lr_admin_role", $RoleAdmin)
        updateParam("lr_bank_role", $RoleBank)
        updateParam("lr_depart2_role", $RoleDepart2)
        updateParam("lr_depart3_role", $RoleDepart3)
    }
}