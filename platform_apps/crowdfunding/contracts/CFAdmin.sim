contract CFAdmin {
    data {
        Id int
        Action string
    }
    conditions{
        $startup = DBFind("@1cf_startups").Where({id: $Id, deleted_at:0}).Row()
        if !$startup{
            warning "Startup not found"
        }

        if $Action == "finish"{
            var ecoAdmin eco int
            eco = Int($startup["ecosystem"])
            if eco != $ecosystem_id {
                warning "For this action need run in startup ecosystem"
            }
            ecoAdmin = Int(EcosysParam("role_admin"))
            if ecoAdmin == 0{
                warning "Admin role not defined in target ecosystem"
            }
            if !RoleAccess(ecoAdmin){
                warning "This action allowed only for startup admin"
            }
        }else{
            var appId admin int
            appId = Int(DBFind("@1applications").Where({ecosystem:1, name:"Crowdfunding"}).One("id"))
            admin = Int(AppParam(appId, "cf_catalog_admin", 1))
            if !RoleAccess(admin){
                warning "This action allowed only for startup catalog admin"
            }
        }
        $CREATED = 1
        $PUBLISHED = 2
        $STARTED = 3
        $FINISHED = 4
        $FAILED = 5
        $STOPPED = 6
        $REJECTED = 7
        $appId = Int(DBFind("@1applications").Where({ecosystem:1, name:"Crowdfunding"}).One("id"))
        $fee = AppParam($appId, "cf_fee", 1)
        var feeWallet string
        feeWallet = AppParam($appId, "cf_fee_wallet", 1)
        $feeKey = AddressToId(feeWallet)
        if $feeKey == 0{
            $feeKey = Int(DBFind("@parameters").Where({ecosystem:1, name:"founder_account"}).One("value"))
        }
        $startupKey = AddressToId($startup["wallet"])
    }
    func notify(body string){
        // оповещение wallet экосистемы стартапа
        var sender int header page eco popup string params map
        sender = 1
        header = Sprintf("Startup (%v, %v) notification", $startup["id"], $startup["name"])
        page = "@1cf_notification"
        eco = $startup["ecosystem"]
        popup = "true"
        @1NotificationsSend("member_id,sender,text_header,text_body,page_name,params_map,eco_id,popup", $startupKey, sender, header, body, page, params, eco, popup)
    }
    func succesfullNotify(investor int){
        var params m map
        investor = Int($note["page_params.investor"])

        params["startup_id"] = $startup["id"]
        params["wallet"] = $startup["wallet"]
        m["params_map"] = JSONEncode(params)
        m["member_id"] = investor
        m["closure_type"] = 1
        m["sender"] = 1
        m["eco_id"] = 1
        m["popup"] = "true"
        m["text_body"] = Sprintf("Startup #%v, amount: %v was succesfull startup", $startup["id"], $startup["amount"])
        m["page_name"] = "@1cf_notification"
        m["text_header"] = "Succesfull startup"
        CallContract("@1NotificationsSend", m)
    }
    func sendTokens(){
        // на созданные кошельки инвесторов перевести токены с платформенного мультикошелька стартапа курс 1:1 
        var items array i lenItems limit recipient sender int it map
        sender = AddressToId($startup["wallet"])
        limit = 10000
        items = DBFind("@1cf_investments").Where({startup_id: $Id, refund_at:0}).Limit(limit)
        lenItems = Len(items)
        while i < lenItems{
            it = items[i]
            recipient = AddressToId(it["investment_wallet"])
            var exists map
            exists = DBFind("@1keys").Where({ecosystem:$ecosystem_id, id:recipient}).Row()
            if !exists{
                var pub string
                pub = PubToHex(DBFind("@1keys").Where({ecosystem:1, id:recipient}).One("pub"))
                @1MembershipAdd("memberskeys", pub)
            }
            @1TokensTransfer("Amount,SenderId,RecipientId,Comment", Money(it["amount"]), sender, recipient, "startup investment")
            DBUpdate("@1cf_investments", Int(it["id"]), {refund_at: "-1"})
            // TODO: оповещение инвестору об успешном окончании кампании стартапа
            i = i + 1
        }
        if lenItems == limit{
            sendTokens()
        }
    }
    action {
        var m map
        if $Action == "stop"{
            m["stopped_at"] = $block_time
            m["status"] = $STOPPED
            notify("Your startup compaign was stopped")

        }elif $Action == "accept"{
            m["published_at"] = $block_time
            m["status"] = $PUBLISHED
            notify("Your request for startup compaign creation was accepted")

        }elif $Action == "reject"{
            m["published_at"] = -1
            m["deleted_at"] = $block_time
            m["status"] = $REJECTED
            notify("Your request for startup compaign creation was rejected")

        }elif $Action == "delete"{
            m["deleted_at"] = $block_time
            m["status"] = $STOPPED
            notify("Your startup compaign was deleted")

        }elif $Action == "finish" {
            m["status"] = $FINISHED
            if $startup["status"] == $STARTED{
                notify("Your startup compaign was finished")
            }

        }elif $Action == "start"{
            m["stopped_at"] = 0
            m["status"] = $PUBLISHED
            notify("Your startup compaign was published")
        }

        if m {
            DBUpdate("@1cf_startups", $Id, m)
        }

        if m["status"] == $FINISHED{
            var feeAmount currentAmount estimatedAmount balance money w map
            currentAmount = Money($startup["current_investment"])
            estimatedAmount = Money($startup["estimated_amount"])
            w["ecosystem"] = $startup["ecosystem"]
            w["id"] = AddressToId($startup["wallet"])
            balance = Money(DBFind("@1keys").Where(w).One("amount"))

            if currentAmount < estimatedAmount || currentAmount > balance {
                // если не набрана сумма - возврат токенов или если собранно меньше чем баланс экосистемы стартапа - возврат токенов
                @1CFStartupRefund("Id", $Id)
            }else{
                // переводим комиссию площадки
                feeAmount = currentAmount / Money(100) * Money($fee)
                @1TokensTransfer("Amount,SenderId,RecipientId,Comment,Ecosystem", feeAmount, $startupKey, $feeKey, "crowdfunding fee", 1)
                // создаем кошельки инвесторов и переводим им их токены
                sendTokens()
            }
        }
    }
}