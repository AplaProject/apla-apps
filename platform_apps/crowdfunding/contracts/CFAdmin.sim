contract CFAdmin {
    data {
        Id int
        Action string
    }
    conditions{
        $startup = DBFind("@1cf_startups").WhereId($Id).Row()
        if !$startup{
            warning "Startup not found"
        }

        // проверка прав на администрирование (role_id == cf_catalog_admin) или
        // если Action==finish проверить что я админ стартапа или владелец кошелька для инвестиций
        if $Action == "finish"{
            var ecoAdmin eco int
            eco = Int($startup["ecosystem"])
            ecoAdmin = Int(DBFind("@1parameters").Where({name:"role_admin", ecosystem:eco}).One("value"))
            if ecoAdmin == 0{
                warning "Admin role not defined in target ecosystem"
            }
            if !DBFind("@1roles_participants").Where({"role->id":ecoAdmin, "member->member_id": $key_id, ecosystem:eco, deleted:0}).Row(){
                if $startup["wallet"] != IdToAddress($key_id){
                    warning "You must be Admin in target ecosystem or owner of wallet investments"
                }
            }
        }else{
            var appId admin int
            appId = Int(DBFind("@1applications").Where({ecosystem:1, name:"Crowdfunding"}).One("id"))
            admin = Int(AppParam(appId, "cf_catalog_admin", 1))
            if !RoleAccess(admin){
                warning "This action allowed only for startup catalog admin role"
            }
        }
        $CREATED = 1
        $PUBLISHED = 2
        $STARTED = 3
        $FINISHED = 4
        $FAILED = 5
        $STOPPED = 6
        $REJECTED = 7
    }

    action {
        var m map
        if $Action == "stop"{
            m["stopped_at"] = $block_time
            m["status"] = $STOPPED

        }elif $Action == "accept"{
            m["published_at"] = $block_time
            m["status"] = $PUBLISHED

        }elif $Action == "reject"{
            m["published_at"] = -1
            m["status"] = $REJECTED

        }elif $Action == "delete"{
            m["deleted_at"] = $block_time
            m["status"] = $STOPPED

        }elif $Action == "finish"{
            // TODO: если не набрана сумма - возврат токинов (алгоритм надо будет уточнить)
            // TODO: если набрана - переводится комиссия площадки
            m["status"] = $FINISHED
        }
        DBUpdate("@1cf_startups", $Id, m)
    }
}