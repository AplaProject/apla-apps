contract CFInvestment { // перевод токенов APLA на кошелек стартапа
    data {
        Id int
        Amount money
    }

    conditions{
        $startup = DBFind("@1cf_startups").WhereId($Id).Row()
        if !$startup{
            warning "Startup not found"
        }
        if $Amount < $startup["minimal_investment"]{
            warning "Too small amount"
        }
        $recipient = AddressToId($startup["wallet"])
        if $recipient == 0{
            warning "Startup wallet not found"
        }
    }

    action {
        var m map
        m["startup_id"] = $Id
        m["investment_wallet"] = $key_id
        m["amount"] = $Amount
        m["created_at"] = $block_time
        DBInsert("@1cf_investments", m)

        var comment string ecosystem int
        comment = "startup investment"
        ecosystem = Int($startup["ecosystem"])
        @1TokensTransfer("Amount,SenderId,RecipientId,Comment,Ecosystem", $Amount, $key_id, $recipient, comment, ecosystem)
    }
}