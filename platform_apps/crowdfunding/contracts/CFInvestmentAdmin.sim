contract CFInvestmentAdmin {
    data {
        Action string
        NotificId int
    }

    conditions{
        $CREATED = 1
        $PUBLISHED = 2
        $STARTED = 3
        $FINISHED = 4
        $FAILED = 5
        $STOPPED = 6
        $REJECTED = 7
       // получить стартап из оповещения
        $note = DBFind("@1notifications").Where({ecosystem:$ecosystem_id, id:$NotificId}).Columns("id,page_params->startup_id,page_params->investor,page_params->amount").Row()
        if !$note{
            warning "Notification not found"
        }
        var startupId int
        startupId = Int($note["page_params.startup_id"])
        $startup = DBFind("@1cf_startups").Where({id:startupId, deleted_at:0}).Row()
        if !$startup{
            warning "Startup not found"
        }
        if !$startup["status"] != $PUBLISHED{
            warning "Startup is not published"
        }
        // проверить права доступа - админ экосистемы стартапа
        var ecoAdmin eco int
        eco = Int($startup["ecosystem"])
        ecoAdmin = Int(EcosysParam("role_admin"))
        if !RoleAccess(ecoAdmin){
            warning "This action allowed only for ecosystem admin role"
        }

    }

    action {
        @1NotificationsProcess("notific_id", $NotificId)
        @1NotificationsClose("notific_id", $NotificId)

        var params m map investor int
        investor = Int($note["page_params.investor"])
        params["startup_id"] = $startup["id"]
        params["wallet"] = $startup["wallet"]
        params["amount"] = $note["page_params.amount"]
        m["params_map"] = JSONEncode(params)
        m["member_id"] = investor
        m["closure_type"] = 1
        m["sender"] = 1
        m["eco_id"] = 1
        m["popup"] = "true"
        m["text_body"] = Sprintf("Startup #%v, amount: %v", $startup["id"], $startup["amount"])

        if $Action == "accept"{
            // создать кошелек в целевой экосистеме (если нет)
            if !DBFind("@1keys").Where({ecosystem:$ecosystem_id, id:investor}){
                var pub string
                pub = PubToHex(DBFind("@1keys").Where({ecosystem:1, id:investor}).One("pub"))
                Println("pub:", pub)
                @1MembershipAdd("memberskeys", pub)
            }
            m["page_name"] = "@1cf_invest_notification"
            m["text_header"] = "Accept investment offer"

        }elif $Action == "reject"{
            m["page_name"] = "@1notifications_show"
            m["text_header"] = "Reject investment offer"
        }
        CallContract("@1NotificationsSend", m)
    }
}