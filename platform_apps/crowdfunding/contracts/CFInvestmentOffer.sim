contract CFInvestmentOffer {
    data {
        Id int
        Amount money
    }

    conditions{
        $CREATED = 1
        $PUBLISHED = 2
        $STARTED = 3
        $FINISHED = 4
        $FAILED = 5
        $STOPPED = 6
        $REJECTED = 7
        $startup = DBFind("@1cf_startups").Where({id:$Id, deleted_at:0, status:$STARTED}).Row()
        if !$startup{
            warning "Startup not found"
        }
        if $Amount < $startup["minimal_investment"]{
            warning "Too small amount"
        }
        var eco int
        eco = Int($startup["ecosystem"])
        $rid = Int(DBFind("@1parameters").Where({name:"role_admin", ecosystem:eco}).Columns("value").One("value"))
        if $rid == 0{
            warning "Admin role in Ecosystem of startup not assigned"
        }
    }

    action {
        // оповещение админу целевой экосистемы
        var closure sender int header body page params eco popup string m map
        closure = 1
        sender = 1
        header = "Investment offer"
        body = Sprintf("Startup #%v, amount: %v", $Id, $Amount)
        page = "@1cf_admin_notification"
        m["startup_id"] = $Id
        m["amount"] = $Amount
        m["investor"] = $key_id
        params = JSONEncode(m)
        eco = $startup["ecosystem"]
        popup = "true"

        @1NotificationsSend("rid,closure_type,sender,text_header,text_body,page_name,params_map,eco_id,popup", $rid, closure, sender, header, body, page, params, eco, popup)
    }
}