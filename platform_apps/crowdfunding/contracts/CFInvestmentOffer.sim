contract CFInvestmentOffer {
    data {
        Id int
        Amount money
    }

    conditions{
        $startup = DBFind("@1startups").WhereId($Id).Row()
        if !$startup{
            warning "Startup not found"
        }
        if $Amount < $startup["minimal_investment"]{
            warning "Too small amount"
        }
        var eco int
        eco = Int($startup["ecosystem"])
        $rid = Int(DBFind("@1parameters").Where({name:"role_admin", ecosystem:eco}).Columns("value").One("value"))
        if $rid == 0{
            warning "Admin role in Ecosystem of startup not assigned"
        }
    }

    action {
        // оповещение админу целевой экосистемы
        var closure sender int header body page paramsEnc eco popup string params map
        closure = 1
        sender = 1
        header = "Invest offer"
        body = Sprintf("Startup #%v, amount: %v", $Id, $Amount)
        page = "@1cf_admin_notification"
        params["startup_id"] = $Id
        params["amount"] = $Amount
        params["investor"] = $key_id
        paramsEnc = JSONEncode(params)
        eco = $startup["ecosystem"]
        popup = "true"

        @1NotificationsSend("rid,closure_type,sender,text_header,text_body,page_name,params_map,eco_id,popup", $rid, closure, sender, header, body, page, paramsEnc, eco, popup)
    }
}