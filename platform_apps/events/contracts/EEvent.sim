contract EEvent {
    data {
        Id int "optional"
        Name string
        Description string
        Type int
        StartingDate string
        StartingTime string
        EventAddress string
        Responsible int
    }

    func dateAddTime(d, t string) string {
        var dt string
        if Contains(d, "T00:00:00Z") {
            d = Replace(d, "T00:00:00Z", "")
        }
        if Size(t) == 5 {
            dt = Sprintf("%v %v:00", d, t)
        }
        return dt
    }

    conditions{
        $app = DBFind("@1applications").Where({"ecosystem": $ecosystem_id, "name": "Events"}).One("id")
        var admin_ids string
        admin_ids = AppParam(Int($app), "events_admin_role", $ecosystem_id)
        var rids array
        rids = JSONDecode("["+admin_ids+"]")
        if !DBFind("@1roles_participants").Where({"ecosystem": $ecosystem_id, "role->id": {"$in": rids}, "member->member_id": $key_id, "deleted": 0}).Row() {
            warning LangRes("@1access_denied", "en")
        }

        if $Id > 0 {
            var event map
            event = DBFind("@1e_events").Where({"id": $Id, "ecosystem": $ecosystem_id}).Row()
            if !event {
                warning LangRes("@1e_event_not_found", "en")
            }
            if event["starting_date"] < $block_time {
                warning LangRes("@1e_event_not_editable", "en")
            }
        }

        $date = UnixDateTime(dateAddTime($StartingDate, $StartingTime))
        if $date == 0 {
            warning LangRes("@1starting_date_invalid", "en")
        }

        if !DBFind("@1keys").Where({"ecosystem": $ecosystem_id, "id": $Responsible}).Row() {
            warning LangRes("@1e_responsible_not_found", "en")
        }
    }

    action {
        var addr map encAddr string
        addr = JSONDecode($EventAddress)
        addr["zoom"] = 11
        encAddr = JSONEncode(addr)

        var m map
        m = {"name": $Name, "description": $Description, "type": $Type, "starting_date": $date, "event_address_json": encAddr, "responsible": $Responsible}
        if $Id > 0 {
            DBUpdate("@1e_events", $Id, m)
        } else {
            m["created_at"] = $block_time
            m["ecosystem"] = $ecosystem_id
            DBInsert("@1e_events", m)
        }
    }
}