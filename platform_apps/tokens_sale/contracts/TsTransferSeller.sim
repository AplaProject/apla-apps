contract TsTransferSeller {
    data {
        SaleId int
        NotificId int
    }
    func getRoleId(name string) int{
        var rid int
        rid = Int(EcosysParam(name))
        if rid == 0{
            warning Sprintf(LangRes("@1role_not_set_application_parameter", "en"), name)
        }
        return rid
    }
    conditions{
        $WAITING_MANAGER = 1
        $WAITING_SELLER = 2
        $FINISHED = 3
        $REJECTED = 4
        $appId = Int(DBFind("@1applications").Where({ecosystem:$ecosystem_id, name:"Tokens sale"}).One("id"))
        if $appId == 0{
            warning LangRes("@1app_not_found", "en")
        }
        $currentRole = getRoleId("role_sale_seller")
        if !RoleAccess($currentRole){
            warning "this action allowed only for role id #" + $currentRole
        }

        $sale = DBFind("ts_sales").Where({id:$SaleId}).Row()
        if !$sale{
            warning "sale not found"
        }
    }

    action {
        @1NotificationsClose("notific_id", $NotificId)
        var s transfer map
        s["key_id_seller"] = $key_id
        s["seller_transferred_at"] = $time
        s["status"] = $FINISHED
        DBUpdate("ts_sales", Int($sale["id"]), s)

        @1TokensSend("Recipient,Amount", $sale["key_id_buyer"], Money($sale["amount_apl"]))
    }
}