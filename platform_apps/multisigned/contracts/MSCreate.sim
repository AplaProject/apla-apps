contract MSCreate {
    data {
        BufferId int
        Limit int
        LimitDate string
        LimitTime string
        AmountMax money
        AmountMaxDay money
        AmountMaxMonth money
        Sign int
        Duration int "optional"
        Quorum int "optional"
        Volume int "optional"
        PoaTemplateId int
    }

    func dateAddTime(d, t string) string {
        var dt string
        if Contains(d, "T00:00:00Z") {
            d = Replace(d, "T00:00:00Z", "")
        }
        if Size(t) == 5 {
            dt = Sprintf("%v %v:00", d, t)
        }
        return dt
    }

    conditions {
        $ROLE_SINGLE = 1
        $ROLE_VOTING = 2
        $NO_LIMIT = 1
        $ONE_SIGN = 1

        $buf = DBFind("@1buffer_data").Where({id:$BufferId, ecosystem:$ecosystem_id}).Columns("value->type,value->units").Row()
        if !$buf {
            warning "Not set roles"
        }
        if Int($buf["value.type"]) == 0 {
            warning "Not set roles"
        }
        $units = JSONDecode($buf["value.units"])

        if $Limit != $NO_LIMIT{
            $limit = UnixDateTime(dateAddTime($LimitDate, $LimitTime))
            if $limit == 0 {
                warning "Invalid Date or Time"
            }
        }

        if $Sign == $ONE_SIGN{
            $multi = -1
        }else{
            // перевод голосованием
            if $Duration == 0 || $Quorum == 0|| $Volume == 0 {
                warning "Invalid parameters of voting"
            }
            $multi = 1
        }

        $PARAMS = ["amount_max", "amount_max_day", "amount_max_month", "voting_duration", "voting_quorum", "voting_volume", "amount_day", "amount_month", "date_day", "date_month", "role_id"]
        $VALUES = [$AmountMax, $AmountMaxDay, $AmountMaxMonth, $Duration, $Quorum, $Volume, 0, 0, 0, 0]
    }

    action {
        var table string
        table = Sprintf("@%vkeys", $ecosystem_id)
        DBUpdate(table, $key_id, {multi:$multi})

        // лимиты ролей пишу в таблицу доверенностей
        var params map i lenUnits int
        lenUnits = Len($units)
        params["DateExpiration"] = $LimitDate
        params["TimeExpiration"] = $LimitTime
        params["TemplateId"] = $PoaTemplateId
        params["ParamArr"] = $PARAMS
        params["Recipient"] = IdToAddress($key_id)
        
        while i < lenUnits{
            params["ValueArr"] = Append($VALUES, $units[i])
            CallContract("@1PoaAdd", params)
            i = i + 1
        }
    }
}
