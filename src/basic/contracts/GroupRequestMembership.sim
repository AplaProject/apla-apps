contract GroupRequestMembership {
    data {
        GroupId int
    }

    conditions {
        $group = DBFind("@1groups").Where({"id": $GroupId}).Columns("id,deleted,group_type,group_name,image_id,creator->account,group_access,group_name").Row()
        if !$group {
            warning LangRes("@1group_not_found", "en")
        }
        if !DBFind("@1keys").Where({"ecosystem": $ecosystem_id, "account": $account_id, "deleted": 0}).Row() {
            warning LangRes("@1account_not_found", "en")
        }
        if $group["deleted"] == 1 {
            warning LangRes("@1group_deleted_request_impossible", "en")
        }
        $check_join = DBFind("@1groups_participants").Where({"deleted": 0, "member->account": $account_id, "groups_info->id": $GroupId, "ecosystem": $ecosystem_id}).Columns("id,deleted,member,member->account,groups_info,groups_info->id,ecosystem").One("id")
        if $check_join {
            warning LangRes("@1already_group", "en")
        }
        $check_send_notific = DBFind("@1notifications").Where({"sender->account": $account_id, "page_params->gr_id": $GroupId, "closed": 0, "ecosystem": $ecosystem_id}).Columns("id,closed,ecosystem,sender,sender->account,page_params->gr_id").One("id")
        if $check_send_notific {
            info LangRes("@1request_to_group", "en")
        }
    }

    action {
        var gr_ids array i int icon header body page ecoName string

        var params map
        params["gr_id"] = $GroupId
        params["account"] = $account_id
        icon = "icon icon-user-follow"
        header = "New request"
        body = "New request to the group " + Str($group["group_name"])
        page = "@1groups_request_view"

        gr_ids = JSONDecode($group["group_access"])
        if Len(gr_ids) > 0 {
            while i < Len(gr_ids) {
                var id_role int
                id_role = Int(gr_ids[i])
                @1NotificationsSend("Rid,Sender,Icon,Header,Body,Page,Params,EcosystemId,Closure", id_role, 1, icon, header, body, page, params, $ecosystem_id, 1)
                i = i + 1
            }
        } else {
            $creator_id = $group["creator.account"]
            @1NotificationsSend("MemberAccount,Sender,Icon,Header,Body,Page,Params,EcosystemId", $creator_id, 1, icon, header, body, page, params, $ecosystem_id)
        }
    }
}
