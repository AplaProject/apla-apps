contract NotificationsProcess {
    data {
        notific_id int
    }
    
    conditions {
        $key_address = IdToAddress($key_id)
        $notify = DBFind("@1notifications").Where({"ecosystem": $ecosystem_id, "id": $notific_id}).Columns("recipient->role_id,sender->type,date_start_processing").Row()
        if !$notify {
            warning LangRes("@1notification_not_found", "en")
        }

        if $notify["sender.type"] == 2{
            if Int($notify["date_start_processing"]) != 0 {
                warning LangRes("@1notification_processing_started_already", "en")
            }
            var rid string
            rid = Int($notify["recipient.role_id"])
            if rid > 0{
                if !DBFind("@1roles_participants").Where({"ecosystem": $ecosystem_id, "role->id": rid, "member->address": $key_address, "deleted": 0}).One("id") {
                    warning LangRes("@1notification_not_part_of_role", "en")
                }
            }
        }else{
            if $notify["sender.type"] != 1 {
                warning LangRes("@1notification_type_not_found", "en")
            }
        }
    }

    action {
        var model map
        model["processing_info"] = {"address":$key_address, "member_id":$key_id}
        model["date_start_processing"] = $block_time
        DBUpdate("@1notifications", $notific_id, model)
    }
}