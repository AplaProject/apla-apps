contract VotingRunTokenRefund {
    data {
        VictimAccount string
        SuspectAccount string
        Amount money
        Note string
    }

    conditions {
        if !DBFind("@1keys").Where({"ecosystem": $ecosystem_id, "account": $VictimAccount, "deleted": 0}).Row() {
            warning LangRes("@1victim_wallet_invalid", "en")
        }
        if !DBFind("@1keys").Where({"ecosystem": $ecosystem_id, "account": $SuspectAccount, "deleted": 0}).Row() {
            warning LangRes("@1attacker_wallet_invalid", "en")
        }

        if $Amount <= 0 {
            warning LangRes("@1amount_invalid", "en")
        }
        if Size($Note) == 0 {
            warning LangRes("@1add_description", "en")
        }

        $app_id = Int(DBFind("@1applications").Where({"ecosystem": $ecosystem_id, "name": "Basic"}).One("id"))
        $templateId = Int(DBFind("@1app_params").Where({"app_id": $app_id, "name": "voting_template_tokenrefund", "ecosystem": $ecosystem_id}).One("value"))
        if $templateId == 0 {
            warning LangRes("@1template_id_not_found", "en")
        }
    }

    action {
        TokensLockoutMember("MemberAccount,Value", $VictimAccount, 1)
        TokensLockoutMember("MemberAccount,Value", $SuspectAccount, 1)
        
        var pars m map
        m["victim_account"] = $VictimAccount
        m["attacker_account"] = $SuspectAccount
        m["amount"] = $Amount
        m["note"] = $Note
        m["consortium_member_account"] = $account_id
        m["blocked_at"] = $block_time
        m["status"] = 1
        m["result"] = 0
        pars["RefundId"] = DBInsert("tokens_refund", m)

        @1VotingTemplateRun("TemplateId,Duration,ContractAcceptParams,ContractRejectParams", $templateId, 14, JSONEncode(pars), JSONEncode(pars))
    }
}