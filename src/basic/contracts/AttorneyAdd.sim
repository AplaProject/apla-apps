contract AttorneyAdd {
    data {
        AttorneyId string
        ContractName string
        DateExpiration string
        ParamsNames array "optional"
        ParamsValues array "optional"
    }
    conditions {
        $AttorneyId = AddressToId($AttorneyId)
        $Attorney = DBRow("keys").WhereId($AttorneyId)
        if !$Attorney {
            warning "Attorney not found"
        }

        $Contract = DBRow("contracts").Where({"name": $ContractName})
        if !$Contract {
            warning "Contract not found"
        }
    }
    action {
        var params map i, id int
        while i < Len($ParamsNames) {
            params[$ParamsNames[i]] = $ParamsValues[i]
            i = i + 1
        }

        var  power_of_attorney map
        power_of_attorney["key_id_attorney"] = $AttorneyId
        power_of_attorney["address"] = $key_id
        power_of_attorney["contract"] = $ContractName
        power_of_attorney["date_expiration"] = "timestamp " + $DateExpiration
        power_of_attorney["params"] = params
        id = DBInsert("power_of_attorney", power_of_attorney)
        
        var notification map
        notification["member_id"] = $AttorneyId
        notification["sender"] = 1
        notification["text_header"] = "You got a new power of attorney"
        notification["page_name"] = "attorney_view"
        notification["params_map"] = JSONEncode({"id": id})
        CallContract("NotificationsSend", notification)
    }
}