contract VotingDelete {
    data {
        VotingId int
    }

    conditions {
        $INVALID = 1
        $WAITING = 2
        $STARTED = 3
        $FINISHED = 4

        var voting map
        voting = DBFind("@1votings").Where({"ecosystem": $ecosystem_id, "id": $VotingId, "creator->account": $account_id, "status": $INVALID}).Columns("id").Row()
        if !voting {
            warning LangRes("@1voting_delete_impossible", "en")
        }
    }

    action {
        DBUpdate("@1votings", $VotingId, {deleted:$block_time})
        var notes array
        notes = DBFind("@1notifications").Where({"ecosystem": $ecosystem_id, "page_name": "@1voting_view", "page_params->voting_id": $VotingId})
        var i int
        while i < Len(notes){
            var note map noteId int
            note = notes[i]
            noteId = Int(note["id"])
            @1NotificationsClose("NotificId", noteId)
            i = i + 1
        }
    }
}