contract TokensRefund {
    data {
        SenderAddress string
        RecipientAddress string
        Amount money
    }

    conditions {
        var prevContract string
        prevContract = $stack[0]
        if Len($stack) > 2 {
            prevContract = $stack[Len($stack) - 2]
        }
        if prevContract != "@1TokensRefundAccept"{
            warning LangRes("@1contract_start_tokensrefundaccept_only", "en")
        }

        $amount = Money($Amount)
        if $amount <= 0 {
            warning LangRes("@1amount_less_equal_zero", "en")
        }
        
        var sender map
        sender = DBFind("@1keys").Where({"ecosystem": $ecosystem_id, "address": $SenderAddress}).Row()
        if !sender{
            warning LangRes("@1sender_invalid", "en")
        }
        if !DBFind("@1keys").Where({"ecosystem": $ecosystem_id, "address": $RecipientAddress}).Row(){
            warning LangRes("@1recipient_invalid", "en")
        }
        if sender["blocked"] == 1 {
            warning LangRes("@1sender_blocked", "en")
        }
        if sender["amount"] < $amount {
            warning Sprintf(LangRes("@1sender_amount_not_enough", "en"), sender["amount"])
        }
    }

    action {
        @1TokensTransfer("Amount,SenderAddress,RecipientAddress,Comment", $amount, $SenderAddress, $RecipientAddress, "Refund tokens")
    }
}