contract AttorneyEdit {
    data {
        Id string "optional"
        AttorneyId string
        ContractName string
        DateExpiration string
        ParamsNames array "optional"
        ParamsValues array "optional"
    }
    conditions {
        $Id = Int($Id)
        $AttorneyId = AddressToId($AttorneyId)
        $PowerOfAttorney = DBRow("power_of_attorney").Where({"id": $Id, "address": $key_id, "delete": 0})
        if !$PowerOfAttorney {
            warning "Power of attorney does not exists"
        }
        $Attorney = DBRow("keys").WhereId($AttorneyId)
        if !$Attorney {
            warning "Attorney not found"
        }
        $Contract = DBRow("contracts").Where({"name": $ContractName})
        if !$Contract {
            warning "Contract not found"
        }
    }
    action {
        var params map i,id int
        while i < Len($ParamsNames) {
            params[$ParamsNames[i]] = $ParamsValues[i]
            i = i + 1
        }
        var  power_of_attorney map
        power_of_attorney["key_id_attorney"] = $AttorneyId
        power_of_attorney["address"] = $key_id
        power_of_attorney["contract"] = $ContractName
        power_of_attorney["date_expiration"] = "timestamp " + $DateExpiration
        power_of_attorney["params"] = params

        DBUpdate("power_of_attorney", $Id, power_of_attorney)
    }
}