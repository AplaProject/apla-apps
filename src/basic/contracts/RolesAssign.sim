contract RolesAssign {
    data {
        Rid int
        MemberAccount string
    }

    func getMemberInfo(account string) map {
        var res member map
        member = DBFind("@1members").Where({"ecosystem": $ecosystem_id, "account": account}).Row()
        if member {
            res["account"] = member["account"]
            res["member_name"] = member["member_name"]
            res["image_id"] = member["image_id"]
        } else {
            if DBFind("@1keys").Where({"ecosystem": $ecosystem_id, "account": account, "deleted": 0}).Row() {
                res["account"] = account
                res["member_name"] = account
                res["image_id"] = "0"
            }
        }
        return res
    }

    conditions {
        $role = DBFind("@1roles").Where({"id": $Rid}).Columns("id,deleted,role_type,role_name,image_id,creator->account").Row()
        if Int($role["role_type"]) == 2 {
            warning LangRes("@1elective_role_voting_only", "en")
        }
        if !$role {
            warning LangRes("@1role_not_found", "en")
        }

        if !DBFind("@1keys").Where({"ecosystem": $ecosystem_id, "account": $MemberAccount, "deleted": 0}).Row() {
            warning LangRes("@1account_not_found", "en")
        }

        if $role["deleted"] == 1 {
            warning LangRes("@1role_deleted_request_impossible", "en")
        }

        var specRules map prev_contract string
        prev_contract = $stack[0]
        if Len($stack) > 2{
            prev_contract = $stack[Len($stack) - 2]
        }
        specRules["@1DelegateRefresh"] = 1
        specRules["@1InvestorRefresh"] = 1

        if specRules[prev_contract] != 1 {
            @1RolesAccessCheck("Rid", $Rid)
        }
    }

    action {
        if DBFind("@1roles_participants").Where({"ecosystem": $ecosystem_id, "role->id": $Rid, "member->account": $MemberAccount, "deleted": 0}).One("id") {
            // do not re-assign and do not warning because this contract may be called for other
            return
        }
        var role member appointer map
        role["id"] = $role["id"]
        role["type"] = $role["role_type"]
        role["name"] = $role["role_name"]
        role["image_id"] = $role["image_id"]

        member = getMemberInfo($MemberAccount)
        appointer = getMemberInfo($account_id)
        var m map
        m["role"] = role
        m["member"] = member
        m["appointed"] = appointer
        m["date_created"] = $block_time
        m["ecosystem"] = $ecosystem_id
        DBInsert("@1roles_participants", m)
    }
}