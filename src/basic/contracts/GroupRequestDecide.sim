contract GroupRequestDecide {
    data {
        NotificId int
        Accept int
    }

    conditions {
        $notific = DBFind("@1notifications").WhereId($NotificId).Columns("id,closed,page_params->gr_id,page_params->account,sender,sender->account,recipient,recipient->role_id").Row()
        if $notific["closed"] == 1 {
            warning LangRes("@1request_already", "en")
        }
    }

    action {
        $gr_id = Int($notific["page_params.gr_id"])
        $account = Int($notific["page_params.account"])
        if $Accept == 1 {
            @1GroupsAssign("GroupId,MemberId", $gr_id, $account)
        }
        var params map
        params["gr_id"] = $gr_id
        if $Accept == 1 {
            params["decide"] = 1
        } else {
            params["decide"] = -1
        }
        var icon header body page ecoName string member i int
        icon = "icon icon-user-follow"
        header = "Request to group"
        body = "Solution for your request"
        page = "@1groups_request_view"
        member = $notific["sender.account"]
        if $notific["recipient.role_id"] > 0 {
            var check_send_notific array v map i int
            check_send_notific = DBFind("@1notifications").Columns("id,closed,ecosystem,sender,sender->account,page_params->gr_id,page_params->account").Where({"sender->account":member,"page_params->gr_id":$gr_id,"page_params->account":member,"closed":0,"ecosystem":$ecosystem_id})
            while i < Len(check_send_notific) {
                v = check_send_notific[i]
                @1NotificationsProcess("NotificId", Int(v["id"]))
                @1NotificationsClose("NotificId", Int(v["id"]))
                i = i + 1
            }
            @1NotificationsSend("MemberId,Sender,Icon,Header,Body,Page,Params,EcosystemId,CurrentRid", member, 2, icon, header, body, page, params, $ecosystem_id, Int($notific["recipient.role_id"]))
        } else{
            @1NotificationsSend("MemberId,Sender,Icon,Header,Body,Page,Params,EcosystemId", member, 1, icon, header, body, page, params, $ecosystem_id)
            @1NotificationsClose("NotificId", $NotificId)
        }
    }
}
