contract VotingUpdate {
    data {
        votingID int
    }

    func safeJsonToMap(m string) map {
        var res map
        if Size(m) > 0 { res = JSONToMap(m) }
        return res
    }

    func percentPartOfTotal(part, total int) int {
        if total == 0{ total = 1 }
        var percent int
        percent = Int((part * 100) / total)
        if percent > 100 { percent = 100 }
        return percent
    }

    conditions {
        $INVALID = 1
        $WAITING = 2
        $STARTED = 3
        $FINISHED = 4

        $voting = DBFind("@1votings").Where({ecosystem:$ecosystem_id, id:$votingID, status:$STARTED}).Columns("flags,voting,progress,progress->number_voters,voting->count_type_voters").Row()
        if !$voting {
            warning LangRes("@1started_voting_not_found", "en")
        }
    }

    action {
        var progress voting map
        progress = safeJsonToMap($voting["progress"])
        voting = safeJsonToMap($voting["voting"])

        var flag_success percent_success percent_voters number_voters quorum volume int
        // increment count of voters
        quorum = Int(voting["quorum"])
        volume = Int(voting["volume"])
        number_voters = Int(progress["number_voters"]) + 1
        if Int($voting["voting.count_type_voters"]) == 0 {
            percent_voters = percentPartOfTotal(number_voters, Int(progress["number_participants"]))
            percent_success = percentPartOfTotal(percent_voters, quorum)
        } else {
            percent_voters = percentPartOfTotal(number_voters, Int(progress["number_participants"]))
            percent_success = percentPartOfTotal(number_voters, quorum)
        }

        // update the flag of success
        if percent_success == 100 {
            flag_success = 1
        }
        progress["percent_voters"] = percent_voters
        progress["percent_success"] = percent_success
        progress["number_voters"] = number_voters

        var m map
        m["progress"] = progress
        m["flags->success"] = flag_success
        
        if percent_success == 100 || percent_voters == 100 {
            m["status"] = $FINISHED
            DBUpdate("@1votings", $votingID, m)
            @1VotingDecisionCheck("votingID", $votingID)
        } else {
            DBUpdate("@1votings", $votingID, m)
        }
    }
}