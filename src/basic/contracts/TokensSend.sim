contract TokensSend {
    data {
        Amount money
        Recipient string
        Comment string "optional"
    }
    func getPermission() {
        var array_permissions array result i int prevContract string
        array_permissions = ["@1TokensSend", "@1TsKeyApprove", "@1TsTransferSeller"]

        prevContract = $stack[0]
        if Len($stack) > 2 {
            prevContract = $stack[Len($stack) - 2]
        }
        while i < Len(array_permissions) {
            var contract_name string
            contract_name = array_permissions[i]
            if contract_name==prevContract {
                result = 1
            }
            i = i + 1
        }

        if result == 0 {
            warning LangRes("@1tokens_transfer_cannot_be_made", "en")
        }
    }

    conditions {
        getPermission()

        $recipient_id = AddressToId($Recipient)
        if $recipient_id == 0 {
            warning Sprintf(LangRes("@1recipient_x_invalid", "en"), IdToAddress($recipient_id))
        }
    }

    action {
        @1TokensTransfer("Amount,SenderId,RecipientId,Comment,Ecosystem", $Amount, $key_id, $recipient_id, $Comment, $ecosystem_id)
    }
}