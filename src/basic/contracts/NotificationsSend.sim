contract NotificationsSend {
    data {
        MemberId string "optional"
        Rid int "optional"
        GroupId int "optional"
        Closure int "optional"
        Sender int
        Icon string "optional"
        Header string
        Body string "optional"
        Page string
        Params string // json encoded expected
        CurrentRid int "optional"
        EcosystemId int "optional"
        Popup string "optional"
    }

    func getMemberInfo(account string, ecoId int) map {
        var res member map
        member = DBFind("@1members").Where({"ecosystem": ecoId, "account": account}).Row()
        if member {
            res["account"] = member["account"]
            res["member_name"] = member["member_name"]
            res["image_id"] = member["image_id"]
        } else {
            if DBFind("@1keys").Where({"ecosystem": ecoId, "account": account, "deleted": 0}).Row() {
                res["account"] = account
                res["member_name"] = account
                res["image_id"] = "0"
            }
        }
        return res
    }

    func sendNotify(recipient map) {
        var notification map
        notification["header"] = $Header
        notification["type"] = $notify_type
        notification["icon"] = $Icon
        notification["body"] = $Body
        if $Popup == "true" || $Popup == "1" {
            notification["popup"] = 1
        } else {
            notification["popup"] = 0
        }
        var sender map
        if $Sender == 1 && $senderMember {
            // send from member
            sender["type"] = $Sender
            sender["account"] = $senderMember["account"]
            sender["member_name"] = $senderMember["member_name"]
            sender["image_id"] = $senderMember["image_id"]
        } else {
            if $Sender == 2 && $senderRole {
                // send from role
                sender["type"] = $Sender
                sender["role_id"] = $senderRole["id"]
                sender["role_name"] = $senderRole["role_name"]
                sender["image_id"] = $senderRole["image_id"]
            }
        }
        var model map
        model["recipient"] = recipient
        model["sender"] = sender
        model["notification"] = notification
        model["page_name"] = $Page
        model["page_params"] = $Params
        model["date_created"] = $block_time
        model["ecosystem"] = $EcosystemId
        DBInsert("@1notifications", model)
    }

    func sendToRole(roleId, ecoId int) {
        var role recipient map
        role = DBFind("@1roles").Where({"ecosystem": ecoId, "id": roleId}).Row()
        recipient["role_id"] = role["id"]
        recipient["role_name"] = role["role_name"]
        recipient["image_id"] = role["image_id"]
        sendNotify(recipient)
    }

    conditions {
        if $EcosystemId == 0 {
            $EcosystemId = $ecosystem_id
        }
        if Size($Icon) < 1 {
            $Icon = "icon-envelope"
        }
        if $MemberId && $MemberId != "0" {
            if !DBFind("@1keys").Where({"ecosystem": $EcosystemId, "account": $MemberId, "deleted": 0}).Row() {
                warning LangRes("@1key_not_found", "en")
            }
            $notify_type = 1
        } elif $Rid > 0 {
            $notify_type = 2
            if !DBFind("@1roles").Where({"ecosystem": $EcosystemId, "id": $Rid}).One("id") {
                warning $this_contract + ": " + LangRes("@1recipient_role_not_found", "en")
            }
            if !DBFind("@1roles_participants").Where({"ecosystem": $EcosystemId, "role->id": $Rid, "deleted": 0}).One("id") {
                warning $this_contract + ": " + LangRes("@1recipient_role_no_members", "en")
            }
        } elif $GroupId > 0 {
            $notify_type = 1
            if !DBFind("@1groups").Where({"ecosystem": $EcosystemId, "id": $GroupId}).One("id") {
                warning $this_contract + ": " + LangRes("@1recipient_group_not_found", "en")
            }
            if !DBFind("@1groups_participants").Where({"ecosystem": $EcosystemId, "groups_info->id": $GroupId, "deleted": 0}).One("id") {
                warning $this_contract + ": " + LangRes("@1recipient_group_no_members", "en")
            }
        } else {
            warning $this_contract + ": " + LangRes("@1notification_type_not_defined", "en")
        }
        $senderMember = nil
        $senderRole = nil
        if $Sender == 1 {
            $senderMember = getMemberInfo($account_id, $ecosystem_id)
        } elif $Sender == 2 {
            if !RoleAccess($CurrentRid) {
                warning LangRes("@1access_denied", "en")
            }
            $senderRole = DBFind("@1roles").Where({"ecosystem": $ecosystem_id, "id": $CurrentRid}).Row()
            if !$senderRole {
                warning $this_contract + ": " + LangRes("@1sender_role_not_found", "en")
            }
        } else {
            warning $this_contract + ": " + LangRes("@1sender_invalid", "en")
        }
    }

    action {
        if $MemberId && $MemberId != "0" {
            sendNotify(getMemberInfo($MemberId, $EcosystemId))
            UpdateNotifications($EcosystemId, $MemberId)
        } elif $Rid > 0 {
            if $Closure == 1 {
                // Single closure: one begins the process - the others do not see
                sendToRole($Rid, $EcosystemId)
                UpdateRolesNotifications($EcosystemId, Int($Rid))
            } else {
                // Multiple closure: to each his own notification
                $notify_type = 1
                var participants array
                participants = DBFind("@1roles_participants").Where({"ecosystem": $EcosystemId, "role->id": $Rid, "deleted": 0}).Columns("member->account")
                var i int
                while i < Len(participants) {
                    var p map
                    p = participants[i]
                    sendNotify(getMemberInfo(p["member.account"], $EcosystemId))
                    UpdateNotifications($EcosystemId, p["member.account"])
                    i = i + 1
                }
            }
        } elif $GroupId > 0 {
            var participants array
            participants = DBFind("@1groups_participants").Where({"ecosystem": $EcosystemId, "groups_info->id": $GroupId, "deleted": 0}).Columns("member->account")
            var i int
            while i < Len(participants) {
                var p map
                p = participants[i]
                sendNotify(getMemberInfo(p["member.account"], $EcosystemId))
                UpdateNotifications($EcosystemId, p["member.account"])
                i = i + 1
            }
        }
    }
}