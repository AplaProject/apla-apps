contract MembershipDecide {
    data {
        notific_id int
        Accept int
    }
    conditions {
        $request = DBFind("@1notifications").Where({ecosystem:$ecosystem_id, id:$notific_id}).Columns("id,closed,sender->member_id,page_params->ecosystem_id").Row()


        if Int($request["closed"]) == 1 {
            warning "Request already processed"
        }

        var founder_id ecoId int
        ecoId = Int($request["page_params.ecosystem_id"])
        founder_id = DBFind("@1parameters").Where({name:"founder_account", ecosystem:ecoId}).One("value")

        if founder_id != $key_id {
            warning "You are not the founder of the ecosystem"
        }
    }
    action {
        var params map
        params["ecosystem_id"] = $request["page_params.ecosystem_id"]
        if ($Accept == 0){
            params["status"] = 1

            var table_name string
            table_name = Sprintf("@%v_keys", $request["page_params.ecosystem_id"])

            var pub string mid int
            mid = Int($request["sender.member_id"])
            pub = DBFind("@1keys").Where({ecosystem:$ecosystem_id, id:mid}).One("pub")
            DBInsert(table_name, {id:mid, pub:pub})
        }
        else{
            params["status"] = 0
        }

        var icon header body page ecoName ecoId string
        ecoId = $request["page_params.ecosystem_id"]
        ecoName = DBFind("@1ecosystems").Where({id:ecoId}).One("name")
        icon = "icon icon-user-follow"
        header = "Request is accepted"
        body = Sprintf("Your request to membership for ecosystem '%v' (%v) is accepted", ecoName, ecoId)
        page = "membership_user_view"

        @1NotificationsSend("member_id,sender,icon_name,text_header,text_body,page_name,params_map", $request["sender.member_id"], 1, icon, header, body, page, params)
        @1NotificationsClose("notific_id", $notific_id)
    }
}