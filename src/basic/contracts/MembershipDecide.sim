contract MembershipDecide {
    data {
        NotificId int
        Accept int
    }

    conditions {
        $system_app_id = DBFind("@1applications").Where({"ecosystem": $ecosystem_id, "name": "System"}).One("id")
        $request = DBFind("@1notifications").Where({"ecosystem": $ecosystem_id, "id": $NotificId}).Columns("id,closed,sender->account,page_params->ecosystem_id,page_params->ecosystem_sender").Row()
        var ecoId int
        ecoId = Int($request["page_params.ecosystem_id"])

        if Int($request["closed"]) == 1 {
            warning LangRes("@1request_processed_already", "en")
        }

        var admin_id int
        admin_id = Int(AppParam(Int($system_app_id), "role_admin", $ecosystem_id))
        if !RoleAccess(admin_id) {
            warning LangRes("@1access_denied", "en")
        }
    }

    action {
        var params map
        params["ecosystem_id"] = $request["page_params.ecosystem_id"]
        if $Accept == 1 {
            params["status"] = 1
            var pub account string eco_id account_key int
            account = $request["sender.account"]
            account_key = AddressToId(account)
            eco_id = Int($request["page_params.ecosystem_id"])
            pub = DBFind("@1keys").Where({"ecosystem": 1, "account": account, "deleted": 0}).One("pub")
            DBInsert("@1keys", {"ecosystem": $ecosystem_id, "id": account_key, "account": account, "pub": pub, "deleted": 0})
        } else {
            params["status"] = 0
        }

        var icon header body page ecoName ecoId string
        ecoId = $request["page_params.ecosystem_id"]
        ecoName = DBFind("@1ecosystems").Where({"id": ecoId}).One("name")
        icon = "icon icon-user-follow"
        header = LangRes("@1request_approved", "en")
        body = Sprintf(LangRes("@1ecosystem_request_approved", "en"), ecoName, ecoId)
        page = "@1membership_user_view"

        @1NotificationsSend("MemberId,Sender,Icon,Header,Body,Page,Params,EcosystemId", $request["sender.account"], 1, icon, header, body, page, params, Int($request["page_params.ecosystem_sender"]))
        @1NotificationsProcess("NotificId", $NotificId)
        @1NotificationsClose("NotificId", $NotificId)
    }
}