contract MembershipDecide {
    data {
        notific_id int
        accept int
    }

    conditions {
        $system_app_id = DBFind("@1applications").Where({"ecosystem": $ecosystem_id, "name": "System"}).One("id")
        $request = DBFind("@1notifications").Where({"ecosystem": $ecosystem_id, "id": $notific_id}).Columns("id,closed,sender->member_id,page_params->ecosystem_id,page_params->ecosystem_sender").Row()
        var ecoId int
        ecoId = Int($request["page_params.ecosystem_id"])

        if Int($request["closed"]) == 1 {
            warning LangRes("@1request_processed_already", "en")
        }

        var admin_id int
        admin_id = Int(AppParam(Int($system_app_id), "role_admin", $ecosystem_id))
        if !RoleAccess(admin_id) {
            warning LangRes("@1access_denied", "en")
        }
    }

    action {
        var params map
        params["ecosystem_id"] = $request["page_params.ecosystem_id"]
        if $accept == 1 {
            params["status"] = 1
            var pub string mid eco_id int
            mid = Int($request["sender.member_id"])
            eco_id = Int($request["page_params.ecosystem_id"])
            pub = DBFind("@1keys").Where({"ecosystem": 1, "id": mid}).One("pub")
            DBInsert("@1keys", {id:mid, pub:pub, ecosystem:$ecosystem_id})
        } else {
            params["status"] = 0
        }

        var icon header body page ecoName ecoId string
        ecoId = $request["page_params.ecosystem_id"]
        ecoName = DBFind("@1ecosystems").Where({"id": ecoId}).One("name")
        icon = "icon icon-user-follow"
        header = LangRes("@1request_approved", "en")
        body = Sprintf(LangRes("@1ecosystem_request_approved", "en"), ecoName, ecoId)
        page = "@1membership_user_view"

        @1NotificationsSend("member_id,sender,icon_name,text_header,text_body,page_name,params_map,eco_id", $request["sender.member_id"], 1, icon, header, body, page, params, Int($request["page_params.ecosystem_sender"]))
        @1NotificationsProcess("notific_id", $notific_id)
        @1NotificationsClose("notific_id", $notific_id)
    }
}