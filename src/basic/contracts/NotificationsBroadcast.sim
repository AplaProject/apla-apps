contract NotificationsBroadcast {
    data {
        RoleId int
        Header string
        Body string
        Popup string "optional"
    }

    conditions {
        if $RoleId !=1 {
            warning LangRes("@1admin_only_action", "en")
        }
        if $ecosystem_id != 1 {
            warning LangRes("@1first_ecosystem_admin_only_action", "en")
        }
        $ecos = DBFind("@1ecosystems").Columns("id")
        $lenEcos = Len($ecos)
        if $lenEcos == 1 {
            warning LangRes("@1other_ecosystems_not_found", "en")
        }
    }

    action {
        $key_address = IdToAddress($key_id)
        var i lenIds int eco map ids array bt string sender recipient params note map
        bt = BlockTime()
        sender["address"] = $key_address
        sender["key_id"] = $key_id
        sender["member_id"] = $key_id
        sender["type"] = 1
        note["header"] = $Header
        note["type"] = 1
        note["icon"] = "icon-envelope"
        note["body"] = $Body
        if $Popup == "true" || $Popup == "1" {
            note["popup"] = 1
        }else{
            note["popup"] = 0
        }

        while i < $lenEcos{
            eco = $ecos[i]
            var ecoId int
            ecoId = Int(eco["id"])
            if ecoId != 1 {
                var adminRoleId int
                adminRoleId = Int(DBFind("@1parameters").Where({"ecosystem": ecoId, "name": "role_admin"}).One("value"))
                ids = DBFind("@1roles_participants").Where({"ecosystem": ecoId, "role->id": adminRoleId, "role->type": 3}).Columns("member->member_id")
                lenIds = Len(ids)
                var i_ids recipientKey int recipientAddress string
                while i_ids < lenIds{
                    recipient = ids[i_ids]
                    recipientKey = Int(recipient["member.member_id"])
                    recipientAddress = IdToAddress(recipientKey)
                    var m map
                    m["recipient"] = {"address":recipientAddress, "key_id":recipientKey, "member_id":recipientKey}
                    m["sender"] = sender
                    m["notification"] = note
                    m["page_name"] = "@1notifications_show"
                    m["page_params"] = params
                    m["date_created"] = bt
                    m["ecosystem"] = ecoId
                    DBInsert("@1notifications", m)
                    i_ids = i_ids + 1
                }
            }
            i = i + 1
        }
    }
}