contract GroupsAssign {
    data {
        GroupId int
        MemberAccount string
    }

    func getMemberInfo(account string) map {
        var res member map
        member = DBFind("@1members").Where({"ecosystem": $ecosystem_id, "account": account}).Row()
        if member {
            res["account"] = member["account"]
            res["member_name"] = member["member_name"]
            res["image_id"] = member["image_id"]
        } else {
            if DBFind("@1keys").Columns("id").Where({"ecosystem": $ecosystem_id, "account": account, "deleted": 0}).Row() {
                res["account"] = account
                res["member_name"] = account
                res["image_id"] = "0"
            }
        }
        return res
    }

    conditions {
        $group = DBFind("@1groups").Where({"id": $GroupId}).Columns("id,deleted,group_type,group_name,image_id,creator->account").Row()
        if !$group {
            warning LangRes("@1group_not_found", "en")
        }
        if !DBFind("@1keys").Where({"ecosystem": $ecosystem_id, "account": $MemberAccount, "deleted": 0}).Row() {
            warning LangRes("@1account_not_found", "en")
        }
        if Int($group["deleted"]) == 1 {
            warning LangRes("@1group_deleted_request_impossible", "en")
        }
        if Int($group["group_type"]) != 1 {
            @1GroupsAccessCheck("GroupId", $GroupId)
        }
    }

    action {
        if DBFind("@1groups_participants").Where({"ecosystem": $ecosystem_id, "groups_info->id": $GroupId, "member->account": $MemberAccount, "deleted": 0}).One("id") {
            // do not re-assign and do not warning because this contract may be called for other
            return
        }

        var group member appointer map
        group["id"] = $group["id"]
        group["type"] = $group["group_type"]
        group["name"] = $group["group_name"]
        group["image_id"] = $group["image_id"]
        member = getMemberInfo($MemberAccount)
        appointer = getMemberInfo($account_id)

        var m map
        m["groups_info"] = group
        m["member"] = member
        m["appointed"] = appointer
        m["date_created"] = $block_time
        m["ecosystem"] = $ecosystem_id
        DBInsert("@1groups_participants", m)
    }
}