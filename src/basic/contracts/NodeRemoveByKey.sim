contract NodeRemoveByKey {
    data {}

    func nodesRemove(key int) array {
        var nodesJSON string nodes res array
        nodesJSON = SysParamString("full_nodes")
        if !HasPrefix(nodesJSON, "[") {
            // empty string nodes
            return res
        }
        nodes = JSONDecode(nodesJSON)
        var i int
        while i < Len(nodes) {
            var node map
            node = nodes[i]
            if node["key_id"] != key {
                res = Append(res, node)
            }
            i = i + 1
        }
        return res
    }

    conditions {
        if $original_contract != "ConsortiumMemberResign" {
            warning LangRes("@1contract_chain_distorted", "en")
        }
    }

    action {
        var nodes array fullNodes string
        nodes = nodesRemove(Int(AddressToId($account_id)))
        fullNodes = JSONEncode(nodes)
        DBUpdateSysParam("full_nodes", fullNodes, "")
    }
}