contract ProfileEdit {
    data {
        Name string
        Info string "optional"
        ImageId string "optional"
    }

    conditions {
        $member = DBFind("@1members").Where({"ecosystem": $ecosystem_id, "id": $key_id}).Row()
        $app_id = DBFind("@1applications").Where({"ecosystem": $ecosystem_id, "name": "Basic", "deleted": 0}).One("id")
        $ImageId = Int($ImageId)

        if !$Name {
            warning LangRes("@1member_name_empty", "en")
        }
        if !$member {
            // check member_name
            if DBFind("@1members").Where({"ecosystem": $ecosystem_id, "member_name": $Name}).One("id") {
                warning LangRes("@1member_name_taken", "en")
            }
        }
    }

    action {
        if $ImageId > 0 {
            $Data = DBFind("@1binaries").Where({"ecosystem": $ecosystem_id, "id": $ImageId}).One("data")
            $ImageId = @1UploadBinary("Name,Data,ApplicationId", "avatar", $Data, Int($app_id))
        }

        var m map
        m["member_info"] = {information: $Info}
        m["image_id"] = $ImageId

        if $member {
            DBUpdateExt("@1members", {"ecosystem": $ecosystem_id, "id": $key_id}, m)
        } else {
            m["id"] = $key_id
            m["member_name"] = $Name
            m["ecosystem"] = $ecosystem_id
            DBInsert("@1members", m)
        }
    }
}