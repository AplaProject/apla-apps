contract ProfileEdit {
    data {
        member_name string
        information string "optional"
    }

    conditions {
        $member = DBFind("@1members").Where({ecosystem:$ecosystem_id, id:$key_id}).Row()
        $app_id = DBFind("@1applications").Where({ecosystem:$ecosystem_id, name:"Basic application", deleted:0}).One("id")

        if !$member {
            // check member_name
            if DBFind("@1members").Where({ecosystem:$ecosystem_id, member_name:$member_name}).One("id") {
                warning "This member name is busy. Enter another member name, please"
            }
        }
    }

    action {
        var m map
        m["member_info"] = {information:$information}
        if $member{
            DBUpdate("@1members", $key_id, m)
        } else {
            m["id"] = $key_id
            m["member_name"] = $member_name
            m["ecosystem"] = $ecosystem_id
            DBInsert("@1members", m)
        }
    }
}