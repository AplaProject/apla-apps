contract VotingSubjectDocument {
    data {
        votingID int
        text_document string

        contract_accept string "optional"
        contract_accept_params string "optional"
        contract_reject string "optional"
        contract_reject_params string "optional"
    }

    func safeJsonToMap(m string) map {
        var res map
        if Size(m) > 0 { res = JSONToMap(m) }
        return res
    }

    conditions {
        $STARTED = 3
        $FINISHED = 4
        $voting = DBFind("@1votings").Where({"ecosystem": $ecosystem_id, "id": $votingID, "status":[{"$neq":$STARTED},{"$neq":$FINISHED}]}).Columns("id,date_started,flags->full_data,voting->type,optional,creator->member_id").Row()
        if !$voting {
            warning LangRes("@1voting_started_can_not_edit", "en")
        }
        if $voting["creator.member_id"] != $key_id {
            warning LangRes("@1access_denied", "en")
        }

        if $voting["flags.full_data"] == 1 && $voting["voting.type"] == 2 {
            warning LangRes("@1system_voting_can_not_edit", "en")
        }
    }

    action {
        var subject map
        subject["text"] = $text_document
        subject["hash"] = Sha256($text_document)

        $subject_id = DBFind("@1votings_subject").Where({"ecosystem": $ecosystem_id, "voting_id": $votingID}).One("id")
        if $subject_id {
            DBUpdate("@1votings_subject", Int($subject_id), {subject:subject})
        } else {
            DBInsert("@1votings_subject", {ecosystem: $ecosystem_id, voting_id:$votingID, subject:subject})
        }
        var accept_params reject_params map
        accept_params = safeJsonToMap($contract_accept_params)
        reject_params = safeJsonToMap($contract_reject_params)

        var optional map
        optional = safeJsonToMap($voting["optional"])

        optional["contract_accept"] = $contract_accept
        optional["contract_accept_params"] = accept_params
        optional["contract_reject"] = $contract_reject
        optional["contract_reject_params"] = reject_params

        DBUpdate("@1votings", $votingID, {optional:optional})

        @1VotingSubjectCheck("votingID", $votingID)
    }
}