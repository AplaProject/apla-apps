contract NewDelayedContract {
    data {
        Contract string
        EveryBlock int
        Conditions string
        BlockID int "optional"
        Limit int "optional"
    }
    conditions {
        $key_address = IdToAddress($key_id)
        ValidateCondition($Conditions, $ecosystem_id)

        if !HasPrefix($Contract, "@") {
            $Contract = "@" + Str($ecosystem_id) + $Contract
        }

        if GetContractByName($Contract) == 0 {
            warning Sprintf(LangRes("@1template_unknown_contract", "en"), $Contract)
        }

        if $BlockID == 0 {
            $BlockID = $block + $EveryBlock
        }

        if $BlockID <= $block {
            warning LangRes("@1blockid_must_greater", "en")
        }
    }
    action {
        DBInsert("@1delayed_contracts", {"contract": $Contract, "key_id": $key_id, "address": $key_address, "block_id": $BlockID,
            "every_block": $EveryBlock, "limit": $Limit, "conditions": $Conditions})
    }
}