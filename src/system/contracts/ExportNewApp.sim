contract ExportNewApp {
    data {
        ApplicationId int
    }

    conditions {
        $key_address = IdToAddress($key_id)
        $app_map = DBFind("@1applications").Columns("id,name").Where({"id": $ApplicationId, "ecosystem": $ecosystem_id}).Row()
        if !$app_map{
            warning LangRes("@1app_not_found", "en")
        }
    }

    action {
        var i int pages_array menu_name_array menu_id_array pages_ret array
        // Search menu
        pages_ret = DBFind("@1pages").Where({"app_id": $ApplicationId, "ecosystem": $ecosystem_id})
        while i < Len(pages_ret) {
            var page_map map
            page_map = pages_ret[i]

            pages_array = Append(pages_array, Sprintf("%v", Str(page_map["menu"])))
            i = i + 1
        }

        if Len(pages_array) > 0 {
            var where_for_menu map
            where_for_menu["name"] = {"$in" : pages_array}

            i = 0
            var menu_ret array
            menu_ret = DBFind("@1menu").Where(where_for_menu)
            while i < Len(menu_ret) {
                var menu_map map
                menu_map = menu_ret[i]

                menu_name_array = Append(menu_name_array, Str(menu_map["name"]))
                menu_id_array = Append(menu_id_array, Str(menu_map["id"]))
                i = i + 1
            }
        }

        // Creating settings
        var value map
        value["app_id"] = Str($ApplicationId)
        value["app_name"] = Str($app_map["name"])

        if Len(menu_name_array) > 0 {
            value["menu_id"] = Str(Join(menu_id_array, ", "))
            value["menu_name"] = Str(Join(menu_name_array, ", "))
            value["count_menu"] = Str(Len(menu_name_array))
        } else {
            value["menu_id"] = "0"
            value["menu_name"] = ""
            value["count_menu"] = "0"
        }

        $buffer_id = DBFind("@1buffer_data").Where({"address": $key_address, "key": "export", "ecosystem": $ecosystem_id}).One("id")
        if !$buffer_id {
            DBInsert("@1buffer_data", {"address":$key_address,"key": "export", "value": value})
        } else {
            DBUpdate("@1buffer_data", Int($buffer_id), {"value": value})
        }
    }
}