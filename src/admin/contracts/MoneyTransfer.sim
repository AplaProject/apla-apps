contract MoneyTransfer {
	data {
		Recipient string
		Amount    string
		Comment     string "optional"
	}
	conditions {
		$recipient = AddressToId($Recipient)
		if $recipient == 0 {
			error Sprintf("Recipient %s is invalid", $Recipient)
		}
		var total money
		$amount = Money($Amount) 
		if $amount <= 0 {
			error "Amount must be greater then zero"
		}

        var row map
        var req money
		row = DBFind("@1_keys").Columns("amount").Where({id: $key_id, ecosystem: $ecosystem_id}).Row()
        total = Money(row["amount"])
        req = $amount + Money(100000000000000000) 
        if req > total {
			error Sprintf("Money is not enough. You have got %v but you should reserve %v", total, req)
		}
	}
	action {
		DBUpdate("keys", $key_id, {"-amount": $amount})
		if DBFind("@1_keys").Columns("id").Where({id: $recipient, ecosystem: $ecosystem_id}).One("id") == nil {
			DBInsert("keys", {"id": $recipient,"amount": $amount})
		} else {
			DBUpdate("keys", $recipient, {"+amount": $amount})
		}
        DBInsert("history", {"sender_id": $key_id,"recipient_id": $recipient,
             "amount":$amount,"comment": $Comment,"block_id": $block,"txhash": $txhash})
	}
}