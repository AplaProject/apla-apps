contract LrDepartChecks {
    data {
        ObjectId int
        NotificId int "optional"
        LegalStatus int "optional"
        EncumbranceCertificate int "optional"
        TaxReceiptAndBills int "optional"
        GovernmentPrice money "optional"
        MarketPrice money "optional"
        RoleId int
    }

    conditions {
        $app_id = DBFind("@1applications").Where({"ecosystem": $ecosystem_id, "name": "Land registry"}).One("id")
        $depart2_role = Int(AppParam(Int($app_id), "lr_depart2_role", $ecosystem_id))
        $depart3_role = Int(AppParam(Int($app_id), "lr_depart3_role", $ecosystem_id))

        if $app_id > 0 {
            if $depart2_role == 0 || $depart3_role == 0 || AppParam(Int($app_id), "lr_admin_role", $ecosystem_id) == 0 || AppParam(Int($app_id), "lr_bank_role", $ecosystem_id) == 0 {
                info LangRes("lr_all_roles_needed", "en")
            }
        } else {
            warning LangRes("@1app_not_found", "en")
        }
    }
    
    action {
        var object status m map

        if $RoleId == $depart2_role {
            object = DBFind("land_registry").WhereId($ObjectId).Row()
            if object["status"] {
                var depart3_status map
                depart3_status = JSONDecode(object["status"])
                status["tax_receipt_and_bills"] = depart3_status["tax_receipt_and_bills"]
            }
            status["legal_status"] = $LegalStatus
            status["encumbrance_certificate"] = $EncumbranceCertificate
            m["modification_date"] = $block_time
            m["status"] = status
            DBUpdate("land_registry", $ObjectId, m)
        }
        if $RoleId == $depart3_role {
            object = DBFind("land_registry").WhereId($ObjectId).Row()
            if object["status"] {
                var depart2_status map
                depart2_status = JSONDecode(object["status"])
                status["legal_status"] = depart2_status["legal_status"]
                status["encumbrance_certificate"] = depart2_status["encumbrance_certificate"]
            }

            status["tax_receipt_and_bills"] = $TaxReceiptAndBills
            m["government_price"] = $GovernmentPrice
            m["market_price"] = $MarketPrice
            m["modification_date"] = $block_time
            m["status"] = status
            DBUpdate("land_registry", $ObjectId, m)
        }

        // checking if the object is ready for sale
        object = DBFind("land_registry").WhereId($ObjectId).Row()
        if Money(object["price"]) > 0 && Money(object["market_price"]) > 0 && Money(object["government_price"]) > 0  {
            var status_map map
            status_map = JSONDecode(object["status"])
            if status_map["legal_status"] == 1 && status_map["encumbrance_certificate"] == 1 && status_map["tax_receipt_and_bills"] == 1 {
                
                // notification to the object owner
                var n params map
                n["sender"] = 1
                n["page_name"] = "lr_view"
                n["icon_name"] = "fa fa-bell-o"
                n["popup"] = "true"
                n["member_id"] = object["owner_id"]
                n["text_header"] = LangRes("lr_object_status", "en")
                n["text_body"] = LangRes("lr_checks_successfull", "en")
                params["object_id"] = $ObjectId
                params["info_message"] = 1
                n["params_map"] = params
                CallContract("@1NotificationsSend", n)
            }
        }

        if $NotificId > 0 {
            var opened int
            opened = DBFind("@1notifications").Where({"id": $NotificId, "closed": 0, "ecosystem": $ecosystem_id}).One("id")
            if opened {
                @1NotificationsProcess("notific_id", $NotificId)
                @1NotificationsClose("notific_id", $NotificId)
            }
        }
    }
}